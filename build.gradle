import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

buildscript {
  ext {
    kotlinVersion = '1.3.40'
    ideaVersion = '2018.3.3'

    dep = [
        kotlinStdLib: "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion",
        kotlinGradlePlugin: "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion",
        intellij: "IC-$ideaVersion",
        intellijCore: fileTree(dir: 'lib/intellij-core/lib', include: [
            'idea.jar', 'openapi.jar', 'util.jar', 'trove4j.jar', 'extensions.jar',
            'picocontainer-1.2.jar', 'asm-all-7.0.jar', 'automaton-1.12-1.jar',
            'platform-api.jar', 'platform-impl.jar', 'java-api.jar', 'guava-25.1-jre.jar',
        ]),
        junit: 'junit:junit:4.12',
        truth: 'com.google.truth:truth:0.30',
    ]
  }
  repositories {
    mavenCentral()
    jcenter()
    maven { url 'https://jitpack.io' }
  }

  dependencies {
    classpath dep.kotlinGradlePlugin
    classpath 'de.undercouch:gradle-download-task:3.1.2'
    classpath "com.github.hurricup:gradle-grammar-kit-plugin:2018.3"
  }
}

apply from: 'updateDependencies.gradle'

allprojects {
  repositories {
    mavenCentral()
    jcenter()
    maven { url 'https://www.jetbrains.com/intellij-repository/releases' }
  }

  group = GROUP
  version = VERSION_NAME

  if (plugins.hasPlugin("org.jetbrains.kotlin.jvm")) {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  tasks.withType(KotlinCompile) {
    kotlinOptions {
      jvmTarget = "1.8"
    }
  }
}
